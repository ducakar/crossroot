# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>

_target=arm-mini2451-linux-musleabi
_cpuflags='--with-cpu=arm926ej-s --with-float=soft'
pkgname=$_target-gcc
pkgver=5.3.0
_islver=0.15
pkgrel=1
pkgdesc="The GNU Compiler Collection - C and C++ frontends for the $_target target"
url='http://gcc.gnu.org'
arch=(i686 x86_64)
license=(GPL)
depends=(bash libmpc $_target-binutils $_target-musl)
source=(ftp://gd.tuwien.ac.at/gnu/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2
	http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2)
md5sums=(c9616fd448f980259c31de613e575719
	 8428efbbc6f6e2810ce5c1ba73ecf98c)

prepare() {
  mkdir -p gcc-$pkgver/build

  ln -s isl-$_islver gcc-$pkgver/isl
}

build() {
  cd gcc-$pkgver/build

  export -n CPPFLAGS

  ../configure --target=$_target \
	       --enable-languages=c \
	       --prefix=/usr \
	       --libexecdir=/usr/lib \
	       --disable-nls \
	       --disable-multilib \
	       --disable-libssp \
	       --disable-static \
	       ${_cpuflags}

  make all-gcc
  make all-target-libgcc
}

package() {
  cd gcc-$pkgver/build

  make install-strip-gcc DESTDIR="$pkgdir"
  make install-strip-target-libgcc DESTDIR="$pkgdir"

  rm -rf "$pkgdir"/usr/{include,share}
  rm -rf "$pkgdir"/usr/lib/gcc/$_target/$pkgver/include-fixed
  $_target-strip "$pkgdir"/usr/$_target/lib/libgcc_s.so.1
}
